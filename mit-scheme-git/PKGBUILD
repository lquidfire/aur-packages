# Maintainer: Edmund Lodewijks <tph@theperfecthobbit.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mit-scheme-git
pkgver=13.0.50.r18935.4809da2af
pkgrel=1
pkgdesc="MIT/GNU Scheme from git"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/mit-scheme/"
license=('GPL-2.0-or-later')
depends=('ncurses' 'libx11')
optdepends=('openssl: support for openssl')
makedepends=('git' 'texlive-latex')
provides=('mit-scheme')
conflicts=('mit-scheme')
#source=("git+https://git.savannah.gnu.org/r/mit-scheme.git")
source=("git+https://git.savannah.gnu.org/git/mit-scheme.git#branch=master")
md5sums=('SKIP')
options=('!makeflags')

pkgver() {
  cd ${pkgname%-git}/src
  ver=$(grep AC_INIT configure.ac | cut -d \[ -f 3 | cut -d \] -f 1).r"$(git rev-list --count HEAD)"
  hsh=$(git rev-parse --short HEAD)
  echo ${ver}.${hsh}
}

build() {
  cd ${pkgname%-git}/src
  export LANG=C
  ./Setup.sh
  export CFLAGS=" -fcommon -Wno-error=use-after-free"
  ./configure --prefix=`pwd`/boot --with-x --enable-x11 
  make
  make install
  make distclean

  ./configure --prefix=/usr --with-x --enable-x11 --enable-native-code
  make
  cd "$srcdir"/${pkgname%-git}/doc
  autoconf
  ./configure --prefix=/usr --enable-pdf --disable-html
  make
}

check() {
  cd ${pkgname%-git}/src
  env FAST=y make check
}

package() {
  cd ${pkgname%-git}/src
  make DESTDIR="$pkgdir/" install
  cd "$srcdir"/${pkgname%-git}/doc
  make DESTDIR="$pkgdir/" install
}
